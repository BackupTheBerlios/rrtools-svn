/***************************************************************************
 *                                                                         *
 *               DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE               *
 *                      Version 2, December 2004                           *
 *                                                                         *
 *   Copyright (C) 2004 Sam Hocevar                                        *
 *    14 rue de Plaisance, 75014 Paris, France                             *
 *   Everyone is permitted to copy and distribute verbatim or modified     *
 *   copies of this license document, and changing it is allowed as long   *
 *   as the name is changed.                                               *
 *                                                                         *
 *              DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE                *
 *     TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION     *
 *                                                                         *
 *   0. You just DO WHAT THE FUCK YOU WANT TO.                             *
 *                                                                         *
 *                                                                         *
 *   This program is free software. It comes without any warranty, to      *
 *   the extent permitted by applicable law. You can redistribute it       *
 *   and/or modify it under the terms of the Do What The Fuck You Want     *
 *   To Public License, Version 2, as published by Sam Hocevar. See        *
 *   http://sam.zoy.org/wtfpl/COPYING for more details.                    *
 *                                                                         *
 ***************************************************************************/

;--------------------------------------
;
; Loader 1541
;
;--------------------------------------

Ld41PsyStart:
    .PSEUDOPC LdSerialEnd

    .(	;*** Ld41 ***

;--------------------------------------

@0Ld41ReadBlock:
	jsr LdSerial_LdSendH
        ldy #0
        sty LdHostDp
Get:
	bit LdHostDp
	bmi Get
        ;ldy #0
	sty LdChk
	jsr GByte
        bne LdSerial_Error
GLoop:
	jsr GByte
	sta LdBf,y
	iny
	bne GLoop
	jsr GByte
	lda LdChk
        bne LdSerial_Error

	ldx LdBf+1
	lda LdBf
	beq Get0
	ldx #$ff
Get0:
	dex
	stx LdChk
        ;clc             ; weg, weil am Ende von Gbyte C=0
	rts

GByte:
	ldx #Serial_Atn
	lda LdHostDp
	stx LdHostDp
	and #$C0
	lsr
	lsr
	sta LdGZp
	jsr GBRts
	ldx #0
	lda LdHostDp
	stx LdHostDp
	and #$C0
	ora LdGZp
	lsr
	lsr
	sta LdGZp
	jsr LdSerial_CSRts
	nop
	ldx #Serial_Atn
	lda LdHostDp
	stx LdHostDp
	and #$C0
	ora LdGZp
	lsr
	lsr
	sta LdGZp
	jsr GBRts
	ldx #0
	lda LdHostDp
	stx LdHostDp
	and #$C0
	ora LdGZp	;3
	pha		;3
	eor LdChk	;3
	sta LdChk	;3
	pla		;4
GBRts:
	rts

;--------------------------------------

    .)	;*** Ld41 ***

Ld41End:
    .REALPC


