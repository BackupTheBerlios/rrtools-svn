/***************************************************************************
 *   Copyright (C) 1998 - 2006 by Christoph Thelen                         *
 *   DocBacardi@the-dreams.de                                              *
 *                                                                         *
 *   This program is free software; you can redistribute it and/or modify  *
 *   it under the terms of the GNU General Public License as published by  *
 *   the Free Software Foundation; either version 2 of the License, or     *
 *   (at your option) any later version.                                   *
 *                                                                         *
 *   This program is distributed in the hope that it will be useful,       *
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of        *
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         *
 *   GNU General Public License for more details.                          *
 *                                                                         *
 *   You should have received a copy of the GNU General Public License     *
 *   along with this program; if not, write to the                         *
 *   Free Software Foundation, Inc.,                                       *
 *   59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.             *
 ***************************************************************************/

;--------------------------------------
;
; IFF V1.1
;
; Drive detection by Ninja
;
; 1.1 : 1570 Support by Baccy
;
;--------------------------------------

#include "iec_lib.src"


#ifndef __IFF_LIB__
__IFF_LIB__ = 1

;--------------------------------------

IFFDevTyp_Unknown	= 0
IFFDevTyp_41		= 1
IFFDevTyp_70		= 2
IFFDevTyp_71		= 3
IFFDevTyp_81		= 4
IFFDevTyp_HD		= 5
IFFDevTyp_FD		= 6
IFFDevTyp_RL		= 7
IFFDevTyp_RD		= 8

;--------------------------------------

    .(	;*** IFF ***

;--------------------------------------

	.segment "zp"

IFFZp_Zp0		.DB 0
IFFZp_Zp1		.DB 0

	.segment "code"

;---------------------------------------

@0IFF:
	lda #6
	ldx #<IFFBSS_Mr0
	ldy #>IFFBSS_Mr0
	jsr iec_lib_SetName
	lda #$6f
	sta iec_libZp_SecAdr
	jsr iec_lib_Open
	bcs DriveOff
	jsr GetByte		;NEW
	tay
	jsr iec_lib_UnTalk
	jsr iec_lib_Close		;NEW
	tya
	ldx #3
CheckFamily:
	cmp IFFBSS_Tab1-1,x
	beq RecognizedFamily
	dex
	bne CheckFamily
	clc
DriveOff:
	rts
RecognizedFamily:
	lda IFFBSS_VLo-1,x
	sta IFFBSS_Mr1+3
	lda IFFBSS_VHi-1,x
	sta IFFBSS_Mr1+4
	lda #6
	ldx #<IFFBSS_Mr1
	ldy #>IFFBSS_Mr1
	jsr iec_lib_SetName
	jsr iec_lib_Open
	jsr GetByte		;NEW
	sta IFFZp_Zp0
	jsr iec_lib_ReadByte
	sta IFFZp_Zp1
	jsr iec_lib_UnTalk
	jsr iec_lib_Close
	ldx #8
CheckTyp:
	lda IFFZp_Zp0
	cmp IFFBSS_TabL-1,x
	bne L3
	lda IFFZp_Zp1
	cmp IFFBSS_TabH-1,x
	beq RecognizedTyp
L3:
	dex
	bne CheckTyp
RecognizedTyp:
	clc
	rts

GetByte:
	lda #0
	sta iec_libZp_90
	lda iec_libZp_Device
	jsr iec_lib_Talk
	lda #$6f
	jsr iec_lib_SecTalk
	jmp iec_lib_ReadByte

;--------------------------------------

IFFBSS_Mr0:
	.PET "m-r"
	.DB $a0,$fe,$01
IFFBSS_Mr1:
	.PET "m-r"
	.DB 0,0,2
IFFBSS_Tab1:
	.DB $43,$0d,$ff
IFFBSS_VLo:
	.DB $a4,$c6,$e9
IFFBSS_VHi:
	.DB $fe,$e5,$a6
IFFBSS_TabL:
	.PET "4778hfrr"
IFFBSS_TabH:
	.DB $b1,$b0,$b1,$b1
	.PET "ddld"

;--------------------------------------

    .)	;*** IFF ***


IFF_DriveTxt:

Drive0_TxtOffs = * - IFF_DriveTxt
	.PET "???",0
Drive1_TxtOffs = * - IFF_DriveTxt
	.PET "1541",0
Drive2_TxtOffs = * - IFF_DriveTxt
	.PET "1570",0
Drive3_TxtOffs = * - IFF_DriveTxt
	.PET "1571",0
Drive4_TxtOffs = * - IFF_DriveTxt
	.PET "1581",0
Drive5_TxtOffs = * - IFF_DriveTxt
	.PET "cmd hd",0
Drive6_TxtOffs = * - IFF_DriveTxt
	.PET "cmd fd",0
Drive7_TxtOffs = * - IFF_DriveTxt
	.PET "cmd ramlink",0
Drive8_TxtOffs = * - IFF_DriveTxt
	.PET "cmd ramdrive",0

IFF_DriveTab:
	.DB Drive0_TxtOffs
	.DB Drive1_TxtOffs
	.DB Drive2_TxtOffs
	.DB Drive3_TxtOffs
	.DB Drive4_TxtOffs
	.DB Drive5_TxtOffs
	.DB Drive6_TxtOffs
	.DB Drive7_TxtOffs
	.DB Drive8_TxtOffs

;--------------------------------------

#endif	; __IFF_LIB__
